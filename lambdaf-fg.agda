-- Formalization of fine-grained Î»F

module LambdaF-FG where

open import Data.Nat using (â„•; zero; suc; _+_)
open import Data.Bool using (true; false) renaming (Bool to ğ”¹)
open import Data.String using (String)
open import Data.Unit using (âŠ¤; tt)
open import Data.Empty using (âŠ¥)
open import Data.Product using (_,_; _Ã—_)
open import Relation.Binary.PropositionalEquality

-- Expression types
data Ty : Set

-- Trail types
data Tr : Set

data Ty where
  Nat          : Ty
  Bool         : Ty
  Str          : Ty
  _â‡’p_         : Ty â†’ Ty â†’ Ty
  _â‡’i_âŸ¨_âŸ©_âŸ¨_âŸ©_ : Ty â†’ Ty â†’ Tr â†’ Ty â†’ Tr â†’ Ty â†’ Ty

data Tr where
  â—      : Tr
  _â‡’âŸ¨_âŸ©_ : Ty â†’ Tr â†’ Ty â†’ Tr

-- Auxiliary relations
-- id-cont-type Ï„ Î¼ Ï„' means the type Ï„ â†’ Î¼ â†’ Ï„' is consistent
-- with the identity continuation
id-cont-type : Ty â†’ Tr â†’ Ty â†’ Set
id-cont-type Ï„ â— Ï„' = Ï„ â‰¡ Ï„'
id-cont-type Ï„ (Ï„â‚ â‡’âŸ¨ Î¼ âŸ© Ï„â‚') Ï„' = Ï„ â‰¡ Ï„â‚ Ã— Ï„' â‰¡ Ï„â‚' Ã— Î¼ â‰¡ â—

-- compatible Î¼â‚ Î¼â‚‚ Î¼â‚ƒ means composing a Î¼â‚-context with a Î¼â‚‚-context
-- results in a Î¼â‚ƒ-context
compatible : Tr â†’ Tr â†’ Tr â†’ Set
compatible â— Î¼â‚‚ Î¼â‚ƒ = Î¼â‚‚ â‰¡ Î¼â‚ƒ
compatible (Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚') â— Î¼â‚ƒ = (Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚') â‰¡ Î¼â‚ƒ
compatible (Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚') (Ï„â‚‚ â‡’âŸ¨ Î¼â‚‚ âŸ© Ï„â‚‚') â— = âŠ¥
compatible (Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚') (Ï„â‚‚ â‡’âŸ¨ Î¼â‚‚ âŸ© Ï„â‚‚') (Ï„â‚ƒ â‡’âŸ¨ Î¼â‚ƒ âŸ© Ï„â‚ƒ') =
  Ï„â‚ â‰¡ Ï„â‚ƒ Ã— Ï„â‚' â‰¡ Ï„â‚ƒ' Ã— compatible (Ï„â‚‚ â‡’âŸ¨ Î¼â‚‚ âŸ© Ï„â‚‚') Î¼â‚ƒ Î¼â‚

-- Values and expressions
-- e : Exp var Ï„ Î¼Î± Î± Î¼Î² Î² means that e
--  * has type Ï„
--  * requires a Ï„-to-Î± context awaiting a Î¼Î±-trail
--  * requires a Î¼Î²-trail
--  * eventually produces a Î²-value
data Val[_]_ (var : Ty â†’ Set) : Ty â†’ Set
data PExp[_]_ (var : Ty â†’ Set) : Ty â†’ Set
data IExp[_]_âŸ¨_âŸ©_âŸ¨_âŸ©_ (var : Ty â†’ Set) : Ty â†’ Tr â†’ Ty â†’ Tr â†’ Ty â†’ Set

data Val[_]_ var where
  Var     : {Ï„ : Ty} â†’ var Ï„ â†’ Val[ var ] Ï„
  Num     : â„• â†’ Val[ var ] Nat
  Bol     : ğ”¹ â†’ Val[ var ] Bool
  Str     : String â†’ Val[ var ] Str
  PAbs    : {Ï„â‚ Ï„â‚‚ : Ty} â†’
            (var Ï„â‚ â†’ PExp[ var ] Ï„â‚‚) â†’
            Val[ var ] (Ï„â‚ â‡’p Ï„â‚‚)
  IAbs    : {Ï„â‚ Ï„â‚‚ Î± Î² : Ty} {Î¼Î± Î¼Î² : Tr} â†’
            (var Ï„â‚ â†’ IExp[ var ] Ï„â‚‚ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î²) â†’
            Val[ var ] (Ï„â‚ â‡’i Ï„â‚‚ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î²)

data PExp[_]_ var where
  Val     : {Ï„ : Ty} â†’ Val[ var ] Ï„ â†’ PExp[ var ] Ï„
  PApp    : {Ï„â‚ Ï„â‚‚ : Ty} â†’
            PExp[ var ] (Ï„â‚ â‡’p Ï„â‚‚) â†’ PExp[ var ] Ï„â‚ â†’
            PExp[ var ] Ï„â‚‚
  PPlus   : PExp[ var ] Nat â†’ PExp[ var ] Nat â†’
            PExp[ var ] Nat
  PIs0    : PExp[ var ] Nat â†’ PExp[ var ] Bool
  PB2S    : PExp[ var ] Bool â†’ PExp[ var ] Str
  Prompt : {Ï„ Î² Î²' : Ty} {Î¼áµ¢ : Tr} â†’
            id-cont-type Î² Î¼áµ¢ Î²' â†’
            IExp[ var ] Î² âŸ¨ Î¼áµ¢ âŸ© Î²' âŸ¨ â— âŸ© Ï„ â†’
            PExp[ var ] Ï„

data IExp[_]_âŸ¨_âŸ©_âŸ¨_âŸ©_ var where
  Exp     : {Ï„ Î± : Ty} {Î¼Î± : Tr} â†’
            PExp[ var ] Ï„ â†’ IExp[ var ] Ï„ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î± âŸ© Î±
  PIApp   : {Ï„â‚ Ï„â‚‚ Î± Î² Î³ : Ty} {Î¼Î± Î¼Î² Î¼Î³ : Tr} â†’
            IExp[ var ] (Ï„â‚ â‡’p Ï„â‚‚) âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î² â†’
            IExp[ var ] Ï„â‚ âŸ¨ Î¼Î³ âŸ© Î³ âŸ¨ Î¼Î± âŸ© Î± â†’
            IExp[ var ] Ï„â‚‚ âŸ¨ Î¼Î³ âŸ© Î³ âŸ¨ Î¼Î² âŸ© Î²
  IApp    : {Ï„â‚ Ï„â‚‚ Î± Î² Î³ Î´ : Ty} {Î¼Î± Î¼Î² Î¼Î³ Î¼Î´ : Tr} â†’
            IExp[ var ] (Ï„â‚ â‡’i Ï„â‚‚ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î²) âŸ¨ Î¼Î³ âŸ© Î³ âŸ¨ Î¼Î´ âŸ© Î´ â†’
            IExp[ var ] Ï„â‚ âŸ¨ Î¼Î² âŸ© Î² âŸ¨ Î¼Î³ âŸ© Î³ â†’
            IExp[ var ] Ï„â‚‚ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î´ âŸ© Î´
  IPlus   : {Î± Î² Î³ : Ty} {Î¼Î± Î¼Î² Î¼Î³ : Tr} â†’
            IExp[ var ] Nat âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î² â†’
            IExp[ var ] Nat âŸ¨ Î¼Î³ âŸ© Î³ âŸ¨ Î¼Î± âŸ© Î± â†’
            IExp[ var ] Nat âŸ¨ Î¼Î³ âŸ© Î³ âŸ¨ Î¼Î² âŸ© Î²
  IIs0    : {Î± Î² : Ty} {Î¼Î± Î¼Î² : Tr} â†’
            IExp[ var ] Nat âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î² â†’
            IExp[ var ] Bool âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î²
  IB2S    : {Î± Î² : Ty} {Î¼Î± Î¼Î² : Tr} â†’
            IExp[ var ] Bool âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î² â†’
            IExp[ var ] Str âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î²
  PControl : {Ï„ Î± Î² Î³ Î³' : Ty} {Î¼áµ¢ Î¼Î± : Tr} â†’
             id-cont-type Î³ Î¼áµ¢ Î³' â†’
             (var (Ï„ â‡’p Î±) â†’ IExp[ var ] Î³ âŸ¨ Î¼áµ¢ âŸ© Î³' âŸ¨ â— âŸ© Î²) â†’
             IExp[ var ] Ï„ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î± âŸ© Î²
  IControl : {Ï„ Î± Î² Î³ Î³' Ï„â‚ Ï„â‚' : Ty} {Î¼áµ¢ Î¼â‚€ Î¼â‚ Î¼â‚‚ Î¼Î± Î¼Î² : Tr} â†’
             id-cont-type Î³ Î¼áµ¢ Î³' â†’
             compatible (Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚') Î¼â‚‚ Î¼â‚€ â†’
             compatible Î¼Î² Î¼â‚€ Î¼Î± â†’
             (var (Ï„ â‡’i Ï„â‚ âŸ¨ Î¼â‚ âŸ© Ï„â‚' âŸ¨ Î¼â‚‚ âŸ© Î±) â†’
              IExp[ var ] Î³ âŸ¨ Î¼áµ¢ âŸ© Î³' âŸ¨ â— âŸ© Î²) â†’
             IExp[ var ] Ï„ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î²


-- CPS interpreter

-- Interpretation of types
ã€š_ã€›Ï„ : Ty â†’ Set
ã€š_ã€›Î¼ : Tr â†’ Set

ã€š Nat ã€›Ï„ = â„•
ã€š Bool ã€›Ï„ = ğ”¹
ã€š Str ã€›Ï„ = String
ã€š Ï„â‚ â‡’p Ï„â‚‚ ã€›Ï„ = ã€š Ï„â‚ ã€›Ï„ â†’ ã€š Ï„â‚‚ ã€›Ï„
ã€š Ï„â‚ â‡’i Ï„â‚‚ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î² ã€›Ï„ =
  ã€š Ï„â‚ ã€›Ï„ â†’ (ã€š Ï„â‚‚ ã€›Ï„ â†’ ã€š Î¼Î± ã€›Î¼ â†’ ã€š Î± ã€›Ï„) â†’ ã€š Î¼Î² ã€›Î¼ â†’ ã€š Î² ã€›Ï„

ã€š â— ã€›Î¼ = âŠ¤
ã€š Ï„ â‡’âŸ¨ Î¼ âŸ© Ï„' ã€›Î¼ = ã€š Ï„ ã€›Ï„ â†’ ã€š Î¼ ã€›Î¼ â†’ ã€š Ï„' ã€›Ï„

-- Identity continuation
kid : {Ï„ Ï„' : Ty} {Î¼ : Tr} â†’
      id-cont-type Ï„ Î¼ Ï„' â†’
      ã€š Ï„ ã€›Ï„ â†’ ã€š Î¼ ã€›Î¼ â†’ ã€š Ï„' ã€›Ï„
kid {Î¼ = â—} refl v tt = v
kid {Î¼ = Ï„â‚ â‡’âŸ¨ .â— âŸ© Ï„â‚'} (refl , refl , refl) v k = k v tt

-- Trail composition
append : {Î¼â‚ Î¼â‚‚ Î¼â‚ƒ : Tr} â†’
         compatible Î¼â‚ Î¼â‚‚ Î¼â‚ƒ â†’ ã€š Î¼â‚ ã€›Î¼ â†’ ã€š Î¼â‚‚ ã€›Î¼ â†’ ã€š Î¼â‚ƒ ã€›Î¼

cons : {Ï„â‚ Ï„â‚' : Ty} {Î¼â‚ Î¼â‚‚ Î¼â‚ƒ : Tr} â†’
       compatible (Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚') Î¼â‚‚ Î¼â‚ƒ â†’ ã€š Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚' ã€›Î¼ â†’ ã€š Î¼â‚‚ ã€›Î¼ â†’ ã€š Î¼â‚ƒ ã€›Î¼

append {â—} refl tt tâ‚‚ = tâ‚‚
append {Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚'} c k tâ‚‚ = cons c k tâ‚‚

cons {Î¼â‚‚ = â—} refl k tt = k
cons {.Ï„â‚} {.Ï„â‚'} {Î¼â‚} {Ï„â‚‚ â‡’âŸ¨ Î¼â‚‚ âŸ© Ï„â‚‚'} {Ï„â‚ â‡’âŸ¨ Î¼â‚ƒ âŸ© Ï„â‚'} (refl , refl , c) k k' =
  Î» v â†’ Î» t' â†’ k v (cons c k' t')

-- is0
is0 : â„• â†’ ğ”¹
is0 zero    = true
is0 (suc _) = false

-- b2s
b2s : ğ”¹ â†’ String
b2s true = "true"
b2s false = "false"

-- Interpretation of values and expressions
cpsv : {Ï„ : Ty} â†’ Val[ ã€š_ã€›Ï„  ] Ï„ â†’ ã€š Ï„ ã€›Ï„

cpsp : {Ï„ : Ty} â†’ PExp[ ã€š_ã€›Ï„  ] Ï„ â†’ ã€š Ï„ ã€›Ï„

cpsi : {Ï„ Î± Î² : Ty} {Î¼Î± Î¼Î² : Tr} â†’
       IExp[ ã€š_ã€›Ï„  ] Ï„ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î² âŸ© Î² â†’
       (ã€š Ï„ ã€›Ï„ â†’ ã€š Î¼Î± ã€›Î¼ â†’ ã€š Î± ã€›Ï„) â†’ ã€š Î¼Î² ã€›Î¼ â†’ ã€š Î² ã€›Ï„

cpsv (Var x) = x
cpsv (Num n) = n
cpsv (Bol b) = b
cpsv (Str s) = s
cpsv (PAbs f) = Î» x â†’ cpsp (f x)
cpsv (IAbs f) = Î» x â†’ cpsi (f x)

cpsp (Val v) = cpsv v
cpsp (PApp eâ‚ eâ‚‚) = (cpsp eâ‚) (cpsp eâ‚‚)
cpsp (PPlus eâ‚ eâ‚‚) = (cpsp eâ‚) + (cpsp eâ‚‚)
cpsp (PIs0 e) = is0 (cpsp e)
cpsp (PB2S e) = b2s (cpsp e)
cpsp (Prompt is-id e) =
  (cpsi e (kid is-id) tt)

cpsi (Exp e) k t = k (cpsp e) t

cpsi (PIApp eâ‚ eâ‚‚) k t =
  cpsi eâ‚
       (Î» vâ‚ tâ‚ â†’ cpsi eâ‚‚ (Î» vâ‚‚ tâ‚‚ â†’ k (vâ‚ vâ‚‚) tâ‚‚) tâ‚)
       t

cpsi (IApp eâ‚ eâ‚‚) k t =
  cpsi eâ‚
       (Î» vâ‚ tâ‚ â†’ cpsi eâ‚‚ (Î» vâ‚‚ tâ‚‚ â†’ vâ‚ vâ‚‚ k tâ‚‚) tâ‚)
       t

cpsi (IPlus eâ‚ eâ‚‚) k t =
  cpsi eâ‚
       (Î» vâ‚ tâ‚ â†’ cpsi eâ‚‚ (Î» vâ‚‚ tâ‚‚ â†’ k (vâ‚ + vâ‚‚) tâ‚‚) tâ‚)
       t

cpsi (IIs0 e) k t =
  cpsi e (Î» v t' â†’ k (is0 v) t') t

cpsi (IB2S e) k t =
  cpsi e (Î» v t' â†’ k (b2s v) t') t

cpsi (PControl is-id f) k t =
  cpsi (f Î» v â†’ k v t) (kid is-id) tt

cpsi (IControl is-id câ‚ câ‚‚ f) k t =
  cpsi (f (Î» v k' t' â†’ k v (append câ‚‚ t (cons câ‚ k' t'))))
       (kid is-id)
       tt

-- Top-level evaluation (proof of Theorem 8)
go : {Ï„ : Ty} â†’ PExp[ ã€š_ã€›Ï„ ] Ï„ â†’ ã€š Ï„ ã€›Ï„
go e = cpsp e


-- Examples and tests
-- âŸ¨ 12 âŸ©
exp0 : {var : Ty â†’ Set} â†’ PExp[ var ] Nat
exp0 =
  Prompt refl (Exp (Val (Num 12)))

test0 : go exp0 â‰¡ 12
test0 = refl

-- âŸ¨ 12 + Fk. k 2 âŸ©
exp1 : {var : Ty â†’ Set} â†’ PExp[ var ] Nat
exp1 =
  Prompt refl
          (IPlus (Exp (Val (Num 12)))
                 (PControl {Ï„ = Nat}
                           refl
                           (Î» k â†’ Exp (PApp (Val (Var k)) (Val (Num 2))))))

test1 : go exp1 â‰¡ 14
test1 = refl

-- 1 + âŸ¨ 2 + Fk. k (k 3) âŸ©
exp2 : {var : Ty â†’ Set} â†’ PExp[ var ] Nat
exp2 =
  PPlus (Val (Num 1))
        (Prompt refl
                 (IPlus (Exp (Val (Num 2)))
                        (PControl {Ï„ = Nat}
                                  refl
                                   (Î» k â†’ Exp (PApp (Val (Var k))
                                              (PApp (Val (Var k)) (Val (Num 3))))))))

test2 : go exp2 â‰¡ 8
test2 = refl

-- Example adapted from Shan [HOSC 2007]
-- (yields different results if control is replaced by
-- shift, shift0, and control0)
-- âŸ¨ âŸ¨ 1 + âŸ¨ (Î» x. Fh. x) (Ff. Fg. 2 + f 5) âŸ© âŸ© âŸ©
exp3 : {var : Ty â†’ Set} â†’ PExp[ var ] Nat
exp3 =
  Prompt refl
    (Exp (Prompt refl
      (Exp (PPlus
        (Val (Num 1))
             (Prompt (refl , refl , refl)
                      (IApp (Exp (Val (IAbs (Î» x â†’
                                   PControl refl
                                            (Î» h â†’ Exp (Val (Var x)))))))
                            (IControl {Î³ = Nat} refl refl refl
                                      (Î» f â†’
                                        PControl refl
                                                 (Î» g â†’
                                                    IPlus (Exp (Val (Num 2)))
                                                          (IApp (Exp (Val (Var f)))
                                                                (Exp (Val (Num 5)))))))))))))

test3 : go exp3 â‰¡ 6
test3 = refl

-- Motivating example from Section 2
-- âŸ¨ (Fkâ‚. is0 (kâ‚ 5)) + (Fkâ‚‚. b2s (kâ‚‚ 8)) âŸ©
exp4 : {var : Ty â†’ Set} â†’ PExp[ var ] Str
exp4 =
  Prompt (refl , refl , refl)
          (IPlus (IControl refl refl refl
                           (Î» kâ‚ â†’ IIs0 (IApp (Exp (Val (Var kâ‚))) (Exp (Val (Num 5))))))
                 (PControl refl
                           (Î» kâ‚‚ â†’ Exp (PB2S (PApp (Val (Var kâ‚‚)) (Val (Num 8)))))))

test4 : go exp4 â‰¡ "false"
test4 = refl

{-
-- Non-terminating example from Kameyama and Yonezawa [FLOPS 2008]
-- (ill-typed in Î»F)
-- âŸ¨ (Fkâ‚. kâ‚ 1; kâ‚ 1); (Fkâ‚‚. kâ‚‚ 1; kâ‚‚ 1) âŸ©
exp5 : {var : Ty â†’ Set} â†’ PExp[ var ] Nat
exp5 =
  Prompt {Î¼áµ¢ = Nat â‡’âŸ¨ â— âŸ© Nat} -- 1
          (refl , refl , refl)
          (IApp {Î¼Î² = Nat â‡’âŸ¨ â— âŸ© Nat} -- 2
                (Exp (Val (IAbs (Î» a â†’
                  IControl {Î¼áµ¢ = â—} -- 3
                           {Î¼â‚€ = Nat â‡’âŸ¨ â— âŸ© Nat} -- 4
                           {Î¼â‚ = â—} -- 5
                           {Î¼â‚‚ = â—} -- 6
                           {Î¼Î± = Nat â‡’âŸ¨ â— âŸ© Nat} -- 7
                           {Î¼Î² = Nat â‡’âŸ¨ â— âŸ© Nat} -- 8
                           refl
                           refl
                           (refl , refl , {!!}) -- (Nat â‡’âŸ¨ â— âŸ© Nat) â‰¡ â—
                           (Î» kâ‚‚ â†’ IApp (Exp (Val (IAbs (Î» c â†’ IApp (Exp (Val (Var kâ‚‚)))
                                                                   (Exp (Val (Num 1)))))))
                                        (IApp (Exp (Val (Var kâ‚‚))) (Exp (Val (Num 1)))))))))
                 (IControl {Î¼áµ¢ = â—} -- 9
                           {Î¼â‚€ = Nat â‡’âŸ¨ â— âŸ© Nat} -- 10
                           {Î¼â‚ = â—} -- 11
                           {Î¼â‚‚ = â—} -- 12
                           {Î¼Î± = Nat â‡’âŸ¨ â— âŸ© Nat} -- 13
                           {Î¼Î² = â—} -- 14
                           refl
                           refl
                           refl
                           (Î» kâ‚ â†’ IApp (Exp (Val (IAbs (Î» b â†’ IApp (Exp (Val (Var kâ‚)))
                                                                    (Exp (Val (Num 1)))))))
                                        (IApp (Exp (Val (Var kâ‚))) (Exp (Val (Num 1)))))))
-}

{-
Let eáµ¢ = káµ¢ 1; káµ¢ 1 where i = 1, 2.

a. By (IControl), initial trail type of eáµ¢ = â—.
   By (IApp), initial trail type of eáµ¢ = initial trail type of body of káµ¢.
   Therefore, 6 = 12 = â—.

b. By (IApp), final trail type of eáµ¢ = final trail type of body of káµ¢.
   Therefore, 3 = 5, 9 = 11.

c. By (IApp), final trail type of first káµ¢ 1 = initial trail type of second káµ¢ 1.
   Therefore, 5 = 6, 11 = 12.

d. By a, c, and compatible (Ï„â‚ â‡’âŸ¨ Î¼â‚ âŸ© Ï„â‚‚) Î¼â‚‚ Î¼â‚€, 4 = 10 = Nat â‡’âŸ¨ â— âŸ© Nat

e. By (Prompt), initial trail type of body of prompt = â—.
   By (IApp), initial trail type of body of prompt = initial trail type of Fkâ‚. eâ‚.
   Therefore, 14 = â—.

f. By compatible Î¼Î² Î¼â‚€ Î¼Î±, 13 = Nat â‡’âŸ¨ â— âŸ© Nat.

g. By (IApp), final trail type of Fkâ‚. eâ‚ = initial trail type of Fkâ‚‚. eâ‚‚.
   Therefore, 8 = Nat â‡’âŸ¨ â— âŸ© Nat.

h. By (Prompt), final trail type of body of prompt must satisfy id-cont-type.
   By (IApp), final trail type of body of prompt = final trail type of Fkâ‚‚. eâ‚‚.
   Therefore, 7 = Nat â‡’âŸ¨ â— âŸ© Nat.

i. By d, g, and h, compatible Î¼Î² Î¼â‚€ Î¼Î± does not hold for Fkâ‚‚. eâ‚‚.
-}


-- Encoding of shift
shift : {var : Ty â†’ Set} {Ï„ Î± Î² Î³ Î³' : Ty} {Î¼Î± Î¼áµ¢ : Tr} â†’
        id-cont-type Î³ Î¼áµ¢ Î³' â†’
        PExp[ var ] ((Ï„ â‡’p Î±) â‡’i Î³ âŸ¨ Î¼áµ¢ âŸ© Î³' âŸ¨ â— âŸ© Î²) â†’
        IExp[ var ] Ï„ âŸ¨ Î¼Î± âŸ© Î± âŸ¨ Î¼Î± âŸ© Î²
shift p f =
  PControl p
    (Î» k â†’ IApp (Exp f)
                (Exp (Val (PAbs (Î» x â†’
                  Prompt refl (Exp (PApp (Val (Var k)) (Val (Var x)))))))))

-- Shan's example
exp5 : {var : Ty â†’ Set} â†’ PExp[ var ] Nat
exp5 =
  Prompt refl (Exp
    (Prompt refl (Exp
      (PPlus
        (Val (Num 1))
             (Prompt {Î² = Nat} refl
                      (IApp (Exp (Val (IAbs (Î» x â†’
                                   shift refl (Val (IAbs (Î» h â†’ Exp (Val (Var x)))))))))
                            (shift {Î³ = Nat} refl
                              (Val
                                (IAbs (Î» f â†’
                                         shift refl
                                           (Val
                                             (IAbs
                                               (Î» g â†’ IPlus (Exp (Val (Num 2)))
                                                            (Exp (PApp (Val (Var f))
                                                                       (Val (Num 5)))))))))))))))))

test5 : go exp5 â‰¡ 8
test5 = refl
